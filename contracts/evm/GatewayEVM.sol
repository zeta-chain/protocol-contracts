// SPDX-License-Identifier: MIT
pragma solidity 0.8.26;

import { INotSupportedMethods } from "../../contracts/Errors.sol";
import {
    MAX_REVERT_GAS_LIMIT,
    RevertContext,
    RevertGasLimitExceeded,
    RevertOptions,
    Revertable
} from "../../contracts/Revert.sol";
import { ZetaConnectorBase } from "./ZetaConnectorBase.sol";
import { IERC20Custody } from "./interfaces/IERC20Custody.sol";
import { Callable, IGatewayEVM, MessageContext } from "./interfaces/IGatewayEVM.sol";
import { GatewayEVMValidations } from "./libraries/GatewayEVMValidations.sol";

import "@openzeppelin/contracts-upgradeable/access/AccessControlUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";
import "@openzeppelin/contracts-upgradeable/proxy/utils/UUPSUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/utils/PausableUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/utils/ReentrancyGuardUpgradeable.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";

import "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";

/// @title GatewayEVM
/// @notice The GatewayEVM contract is the endpoint to call smart contracts on external chains.
/// @dev The contract doesn't hold any funds and should never have active allowances.
contract GatewayEVM is
    Initializable,
    AccessControlUpgradeable,
    UUPSUpgradeable,
    IGatewayEVM,
    ReentrancyGuardUpgradeable,
    PausableUpgradeable,
    INotSupportedMethods
{
    using SafeERC20 for IERC20;
    using GatewayEVMValidations for *;

    /// @notice The address of the custody contract.
    address public custody;
    /// @notice The address of the TSS (Threshold Signature Scheme) contract.
    address public tssAddress;
    /// @notice The address of the ZetaConnector contract.
    address public zetaConnector;
    /// @notice The address of the Zeta token contract.
    address public zetaToken;

    /// @notice Fee charged for additional cross-chain actions within the same transaction.
    /// @dev The first action in a transaction is free, subsequent actions incur this fee.
    /// @dev This is configurable by the admin role to allow for fee adjustments.
    uint256 public additionalActionFeeWei;

    /// @notice New role identifier for tss role.
    bytes32 public constant TSS_ROLE = keccak256("TSS_ROLE");
    /// @notice New role identifier for asset handler role.
    bytes32 public constant ASSET_HANDLER_ROLE = keccak256("ASSET_HANDLER_ROLE");
    /// @notice New role identifier for pauser role.
    bytes32 public constant PAUSER_ROLE = keccak256("PAUSER_ROLE");
    /// @notice Max size of payload + revertOptions revert message.
    uint256 public constant MAX_PAYLOAD_SIZE = 2880;

    /// @notice Storage slot key for tracking transaction action count.
    /// @dev Uses transient storage (tload/tstore) for gas efficiency.
    /// @dev Value 0x01 is used as a unique identifier for this storage slot.
    uint256 private constant _TRANSACTION_ACTION_COUNT_KEY = 0x01;

    /// @custom:oz-upgrades-unsafe-allow constructor
    constructor() {
        _disableInitializers();
    }

    /// @notice Initialize with tss address. address of zeta token and admin account set as DEFAULT_ADMIN_ROLE.
    /// @dev Using admin to authorize upgrades and pause, and tss for tss role.
    function initialize(address tssAddress_, address zetaToken_, address admin_) public initializer {
        if (tssAddress_ == address(0) || zetaToken_ == address(0)) {
            revert ZeroAddress();
        }
        __UUPSUpgradeable_init();
        __ReentrancyGuard_init();
        __AccessControl_init();
        __Pausable_init();

        _grantRole(DEFAULT_ADMIN_ROLE, admin_);
        _grantRole(PAUSER_ROLE, admin_);
        _grantRole(TSS_ROLE, tssAddress_);
        tssAddress = tssAddress_;

        zetaToken = zetaToken_;
    }

    /// @dev Authorizes the upgrade of the contract, sender must be owner.
    /// @param newImplementation Address of the new implementation.
    function _authorizeUpgrade(address newImplementation) internal override onlyRole(DEFAULT_ADMIN_ROLE) { }

    /// @notice Update tss address
    /// @param newTSSAddress new tss address
    function updateTSSAddress(address newTSSAddress) external onlyRole(DEFAULT_ADMIN_ROLE) {
        if (newTSSAddress == address(0)) revert ZeroAddress();

        _revokeRole(TSS_ROLE, tssAddress);
        _grantRole(TSS_ROLE, newTSSAddress);

        emit UpdatedGatewayTSSAddress(tssAddress, newTSSAddress);

        tssAddress = newTSSAddress;
    }

    /// @notice Pause contract.
    function pause() external onlyRole(PAUSER_ROLE) {
        _pause();
    }

    /// @notice Unpause contract.
    function unpause() external onlyRole(PAUSER_ROLE) {
        _unpause();
    }

    /// @notice Update the additional action fee.
    /// @dev Only callable by admin role. This allows for fee adjustments based on network conditions.
    /// @dev Setting fee to 0 disables additional action fees entirely.
    /// @param newFeeWei The new fee amount in wei for additional actions in the same transaction.
    /// @dev Fee should be adjusted based on the chain's native token decimals.
    function updateAdditionalActionFee(uint256 newFeeWei) external onlyRole(DEFAULT_ADMIN_ROLE) {
        uint256 oldFee = additionalActionFeeWei;
        additionalActionFeeWei = newFeeWei;
        emit UpdatedAdditionalActionFee(oldFee, newFeeWei);
    }

    /// @notice Transfers msg.value to destination contract and executes it's onRevert function.
    /// @dev This function can only be called by the TSS address and it is payable.
    /// @param destination Address to call.
    /// @param data Calldata to pass to the call.
    function executeRevert(
        address destination,
        bytes calldata data,
        RevertContext calldata revertContext
    )
        public
        payable
        nonReentrant
        onlyRole(TSS_ROLE)
        whenNotPaused
    {
        GatewayEVMValidations.validateNonZeroAddress(destination);
        (bool success,) = destination.call{ value: msg.value }("");
        if (!success) revert ExecutionFailed();
        Revertable(destination).onRevert(revertContext);

        emit Reverted(destination, address(0), msg.value, data, revertContext);
    }

    /// @notice Executes a call to a destination address without ERC20 tokens.
    /// @dev This function can only be called by the TSS address and it is payable.
    /// @param messageContext Message context containing sender.
    /// @param destination Address to call.
    /// @param data Calldata to pass to the call.
    /// @return The result of the call.
    function execute(
        MessageContext calldata messageContext,
        address destination,
        bytes calldata data
    )
        external
        payable
        nonReentrant
        onlyRole(TSS_ROLE)
        whenNotPaused
        returns (bytes memory)
    {
        GatewayEVMValidations.validateNonZeroAddress(destination);
        bytes memory result;
        // Execute the call on the target contract
        // if sender is provided in messageContext call is authenticated and target is Callable.onCall
        // otherwise, call is arbitrary
        if (messageContext.sender == address(0)) {
            result = _executeArbitraryCall(destination, data);
        } else {
            result = _executeAuthenticatedCall(messageContext, destination, data);
        }

        emit Executed(destination, msg.value, data);

        return result;
    }

    /// @notice Executes a call to a destination contract using ERC20 tokens.
    /// @dev This function can only be called by the custody or connector address.
    ///      It uses the ERC20 allowance system, resetting gateway allowance at the end.
    /// @param messageContext Message context containing sender.
    /// @param token Address of the ERC20 token.
    /// @param to Address of the contract to call.
    /// @param amount Amount of tokens to transfer.
    /// @param data Calldata to pass to the call.
    function executeWithERC20(
        MessageContext calldata messageContext,
        address token,
        address to,
        uint256 amount,
        bytes calldata data
    )
        public
        nonReentrant
        onlyRole(ASSET_HANDLER_ROLE)
        whenNotPaused
    {
        GatewayEVMValidations.validateAmount(amount);
        GatewayEVMValidations.validateNonZeroAddress(to);
        // Approve the target contract to spend the tokens
        if (!_resetApproval(token, to)) revert ApprovalFailed(token, to);
        // Approve token to spender
        IERC20(token).forceApprove(to, amount);
        // Execute the call on the target contract
        // if sender is provided in messageContext call is authenticated and target is Callable.onCall
        // otherwise, call is arbitrary
        if (messageContext.sender == address(0)) {
            _executeArbitraryCall(to, data);
        } else {
            _executeAuthenticatedCall(messageContext, to, data);
        }

        // Reset approval
        if (!_resetApproval(token, to)) revert ApprovalFailed(token, to);

        // Transfer any remaining tokens back to the custody/connector contract
        uint256 remainingBalance = IERC20(token).balanceOf(address(this));
        if (remainingBalance > 0) {
            _transferToAssetHandler(token, remainingBalance);
        }

        emit ExecutedWithERC20(token, to, amount, data);
    }

    /// @notice Directly transfers ERC20 tokens and calls onRevert.
    /// @dev This function can only be called by the custody or connector address.
    /// @param token Address of the ERC20 token.
    /// @param to Address of the contract to call.
    /// @param amount Amount of tokens to transfer.
    /// @param data Calldata to pass to the call.
    /// @param revertContext Revert context to pass to onRevert.
    function revertWithERC20(
        address token,
        address to,
        uint256 amount,
        bytes calldata data,
        RevertContext calldata revertContext
    )
        external
        nonReentrant
        onlyRole(ASSET_HANDLER_ROLE)
        whenNotPaused
    {
        GatewayEVMValidations.validateAmount(amount);
        GatewayEVMValidations.validateNonZeroAddress(to);

        IERC20(token).safeTransfer(address(to), amount);
        Revertable(to).onRevert(revertContext);

        emit Reverted(to, token, amount, data, revertContext);
    }

    /// @notice Deposits ETH to the TSS address.
    /// @param receiver Address of the receiver.
    /// @param revertOptions Revert options.
    /// @dev This function only works for the first action in a transaction (backward compatibility).
    /// @dev For subsequent actions, use the overloaded version with amount parameter.
    function deposit(address receiver, RevertOptions calldata revertOptions) external payable whenNotPaused {
        GatewayEVMValidations.validateDepositParams(receiver, msg.value, revertOptions);

        // Check if this is a subsequent action (action index > 0)
        uint256 currentIndex = _getNextActionIndex();
        if (currentIndex > 0) {
            revert AdditionalActionDisabled();
        }

        // Legacy behavior: transfer entire msg.value to TSS (no fee processing)
        (bool deposited,) = tssAddress.call{ value: msg.value }("");

        if (!deposited) revert DepositFailed();

        emit Deposited(msg.sender, receiver, msg.value, address(0), "", revertOptions);
    }

    /// @notice Deposits ETH to the TSS address with specified amount.
    /// @param receiver Address of the receiver.
    /// @param amount Amount of ETH to deposit (excluding fees).
    /// @param revertOptions Revert options.
    /// @dev msg.value must equal amount + required fee for the action.
    function deposit(
        address receiver,
        uint256 amount,
        RevertOptions calldata revertOptions
    )
        external
        payable
        whenNotPaused
    {
        GatewayEVMValidations.validateDepositParams(receiver, amount, revertOptions);

        uint256 feeCharged = _processFee();
        _validateChargedFeeForETHWithAmount(amount, feeCharged);

        (bool deposited,) = tssAddress.call{ value: amount }("");

        if (!deposited) revert DepositFailed();

        emit Deposited(msg.sender, receiver, amount, address(0), "", revertOptions);
    }

    /// @notice Deposits ERC20 tokens to the custody or connector contract.
    /// @param receiver Address of the receiver.
    /// @param amount Amount of tokens to deposit.
    /// @param asset Address of the ERC20 token.
    /// @param revertOptions Revert options.
    function deposit(
        address receiver,
        uint256 amount,
        address asset,
        RevertOptions calldata revertOptions
    )
        external
        payable
        whenNotPaused
    {
        GatewayEVMValidations.validateDepositParams(receiver, amount, revertOptions);

        uint256 feeCharged = _processFee();
        _validateChargedFeeForERC20(feeCharged);

        _transferFromToAssetHandler(msg.sender, asset, amount);

        emit Deposited(msg.sender, receiver, amount, asset, "", revertOptions);
    }

    /// @notice Deposits ETH to the TSS address and calls an omnichain smart contract.
    /// @param receiver Address of the receiver.
    /// @param payload Calldata to pass to the call.
    /// @param revertOptions Revert options.
    /// @dev This function only works for the first action in a transaction (backward compatibility).
    /// @dev For subsequent actions, use the overloaded version with amount parameter.
    function depositAndCall(
        address receiver,
        bytes calldata payload,
        RevertOptions calldata revertOptions
    )
        external
        payable
        whenNotPaused
    {
        GatewayEVMValidations.validateDepositAndCallParams(receiver, msg.value, payload, revertOptions);

        // Check if this is a subsequent action (action index > 0)
        uint256 currentIndex = _getNextActionIndex();
        if (currentIndex > 0) {
            revert AdditionalActionDisabled();
        }

        // Legacy behavior: transfer entire msg.value to TSS (no fee processing)
        (bool deposited,) = tssAddress.call{ value: msg.value }("");

        if (!deposited) revert DepositFailed();

        emit DepositedAndCalled(msg.sender, receiver, msg.value, address(0), payload, revertOptions);
    }

    /// @notice Deposits ETH to the TSS address and calls an omnichain smart contract with specified amount.
    /// @param receiver Address of the receiver.
    /// @param amount Amount of ETH to deposit (excluding fees).
    /// @param payload Calldata to pass to the call.
    /// @param revertOptions Revert options.
    /// @dev msg.value must equal amount + required fee for the action.
    function depositAndCall(
        address receiver,
        uint256 amount,
        bytes calldata payload,
        RevertOptions calldata revertOptions
    )
        external
        payable
        whenNotPaused
    {
        GatewayEVMValidations.validateDepositAndCallParams(receiver, amount, payload, revertOptions);

        uint256 feeCharged = _processFee();
        _validateChargedFeeForETHWithAmount(amount, feeCharged);

        (bool deposited,) = tssAddress.call{ value: amount }("");

        if (!deposited) revert DepositFailed();

        emit DepositedAndCalled(msg.sender, receiver, amount, address(0), payload, revertOptions);
    }

    /// @notice Deposits ERC20 tokens to the custody or connector contract and calls an omnichain smart contract.
    /// @param receiver Address of the receiver.
    /// @param amount Amount of tokens to deposit.
    /// @param asset Address of the ERC20 token.
    /// @param payload Calldata to pass to the call.
    /// @param revertOptions Revert options.
    function depositAndCall(
        address receiver,
        uint256 amount,
        address asset,
        bytes calldata payload,
        RevertOptions calldata revertOptions
    )
        external
        payable
        whenNotPaused
    {
        GatewayEVMValidations.validateDepositAndCallParams(receiver, amount, payload, revertOptions);

        uint256 feeCharged = _processFee();
        _validateChargedFeeForERC20(feeCharged);

        _transferFromToAssetHandler(msg.sender, asset, amount);

        emit DepositedAndCalled(msg.sender, receiver, amount, asset, payload, revertOptions);
    }

    /// @notice Calls an omnichain smart contract without asset transfer.
    /// @param receiver Address of the receiver.
    /// @param payload Calldata to pass to the call.
    /// @param revertOptions Revert options.
    function call(
        address receiver,
        bytes calldata payload,
        RevertOptions calldata revertOptions
    )
        external
        payable
        whenNotPaused
    {
        GatewayEVMValidations.validateCallParams(receiver, payload, revertOptions);

        uint256 feeCharged = _processFee();
        _validateChargedFeeForERC20(feeCharged);

        emit Called(msg.sender, receiver, payload, revertOptions);
    }

    /// @notice Sets the custody contract address.
    /// @param custody_ Address of the custody contract.
    function setCustody(address custody_) external onlyRole(DEFAULT_ADMIN_ROLE) {
        if (custody_ == address(0)) revert ZeroAddress();
        if (custody != address(0)) revert CustodyInitialized();

        _grantRole(ASSET_HANDLER_ROLE, custody_);
        custody = custody_;
    }

    /// @notice Sets the connector contract address.
    /// @param zetaConnector_ Address of the connector contract.
    function setConnector(address zetaConnector_) external onlyRole(DEFAULT_ADMIN_ROLE) {
        if (zetaConnector_ == address(0)) revert ZeroAddress();
        if (zetaConnector != address(0)) revert ConnectorInitialized();

        _grantRole(ASSET_HANDLER_ROLE, zetaConnector_);
        zetaConnector = zetaConnector_;
    }

    /// @notice Resets the approval of a token for a specified address.
    /// This is used to ensure that the approval is set to zero before setting it to a new value.
    /// @param token Address of the ERC20 token.
    /// @param to Address to reset the approval for.
    /// @return True if the approval reset was successful or if the token reverts on zero approval.
    function _resetApproval(address token, address to) private returns (bool) {
        // Use low-level call to handle tokens that don't return boolean values
        (bool success, bytes memory returnData) = token.call(abi.encodeWithSelector(IERC20.approve.selector, to, 0));
        if (!success) {
            // If the call failed, it might be because the token reverts on zero approval
            // In this case, we consider it successful since the goal is to reset approval
            return true;
        }

        // If there's return data, check if it's true (for standard ERC20 tokens)
        // If there's no return data, assume success (for non-standard tokens like USDT)
        if (returnData.length > 0) {
            bool approved = abi.decode(returnData, (bool));
            return approved;
        }

        return true;
    }

    /// @dev Transfers tokens from the sender to the asset handler.
    /// This function handles the transfer of tokens to either the connector or custody contract based on the asset
    /// type.
    /// @param from Address of the sender.
    /// @param token Address of the ERC20 token.
    /// @param amount Amount of tokens to transfer.
    function _transferFromToAssetHandler(address from, address token, uint256 amount) private {
        if (token == zetaToken) {
            // transfer amount to gateway
            IERC20(token).safeTransferFrom(from, address(this), amount);
            // approve connector to handle tokens depending on connector version (eg. lock or burn)
            if (!IERC20(token).approve(zetaConnector, amount)) revert ApprovalFailed(token, zetaConnector);
            // send tokens to connector
            ZetaConnectorBase(zetaConnector).deposit(amount);
        } else {
            // transfer to custody
            if (!IERC20Custody(custody).whitelisted(token)) revert NotWhitelistedInCustody(token);
            IERC20(token).safeTransferFrom(from, custody, amount);
        }
    }

    /// @dev Transfers tokens to the asset handler.
    /// This function handles the transfer of tokens to either the connector or custody contract based on the asset
    /// type.
    /// @param token Address of the ERC20 token.
    /// @param amount Amount of tokens to transfer.
    function _transferToAssetHandler(address token, uint256 amount) private {
        if (token == zetaToken) {
            // approve connector to handle tokens depending on connector version (eg. lock or burn)
            if (!IERC20(token).approve(zetaConnector, amount)) revert ApprovalFailed(token, zetaConnector);
            // send tokens to connector
            ZetaConnectorBase(zetaConnector).deposit(amount);
        } else {
            // transfer to custody
            if (!IERC20Custody(custody).whitelisted(token)) revert NotWhitelistedInCustody(token);
            IERC20(token).safeTransfer(custody, amount);
        }
    }

    /// @dev Private function to execute an arbitrary call to a destination address.
    /// @param destination Address to call.
    /// @param data Calldata to pass to the call.
    /// @return The result of the call.
    function _executeArbitraryCall(address destination, bytes calldata data) private returns (bytes memory) {
        _revertIfOnCallOrOnRevert(data);
        (bool success, bytes memory result) = destination.call{ value: msg.value }(data);
        if (!success) revert ExecutionFailed();

        return result;
    }

    /// @dev Private function to execute an authenticated call to a destination address.
    /// @param messageContext Message context containing sender and arbitrary call flag.
    /// @param destination Address to call.
    /// @param data Calldata to pass to the call.
    /// @return The result of the call.
    function _executeAuthenticatedCall(
        MessageContext calldata messageContext,
        address destination,
        bytes calldata data
    )
        private
        returns (bytes memory)
    {
        return Callable(destination).onCall{ value: msg.value }(messageContext, data);
    }

    // @dev prevent spoofing onCall and onRevert functions
    function _revertIfOnCallOrOnRevert(bytes calldata data) private pure {
        if (data.length >= 4) {
            bytes4 functionSelector;
            assembly {
                functionSelector := calldataload(data.offset)
            }

            if (functionSelector == Callable.onCall.selector) {
                revert NotAllowedToCallOnCall();
            }

            if (functionSelector == Revertable.onRevert.selector) {
                revert NotAllowedToCallOnRevert();
            }
        }
    }

    /// @notice Processes fee collection for cross-chain actions within a transaction.
    /// @dev The first action in a transaction is free, subsequent actions incur ADDITIONAL_ACTION_FEE_WEI.
    /// @dev If fee is 0, the entire functionality is disabled and will revert.
    /// @return The fee amount actually charged (0 for first action, ADDITIONAL_ACTION_FEE_WEI for
    /// subsequent actions).
    function _processFee() internal returns (uint256) {
        uint256 actionIndex = _getNextActionIndex();

        // First action is free
        if (actionIndex == 0) {
            return 0;
        }

        // If fee is 0, functionality is disabled
        if (additionalActionFeeWei == 0) {
            revert AdditionalActionDisabled();
        }

        // Subsequent actions require fee payment
        if (msg.value < additionalActionFeeWei) {
            revert InsufficientFee(additionalActionFeeWei, msg.value);
        }

        // Transfer fee to TSS address
        (bool success,) = tssAddress.call{ value: additionalActionFeeWei }("");
        if (!success) {
            revert FeeTransferFailed();
        }

        return additionalActionFeeWei;
    }

    /// @notice Validates fee payment for ERC20 operations (deposit, depositAndCall, call).
    /// @dev Validates that msg.value equals the required fee (no excess ETH allowed).
    /// @param feeCharged The fee amount that was charged.
    function _validateChargedFeeForERC20(uint256 feeCharged) internal view {
        // For ERC20 operations, msg.value must equal the required fee
        if (msg.value > feeCharged) {
            revert ExcessETHProvided(feeCharged, msg.value);
        }
    }

    /// @notice Validates fee payment for ETH operations with specified amount.
    /// @dev Validates that msg.value equals amount + feeCharged.
    /// @param amount The amount to deposit (excluding fees).
    /// @param feeCharged The fee amount that was charged.
    function _validateChargedFeeForETHWithAmount(uint256 amount, uint256 feeCharged) internal view {
        uint256 expectedValue = amount + feeCharged;
        if (msg.value != expectedValue) {
            revert IncorrectValueProvided(expectedValue, msg.value);
        }
    }

    /// @notice Gets and increments the transaction action counter using transient storage.
    /// @dev Uses assembly for gas efficiency with tload/tstore operations.
    /// @dev Transient storage is transaction-scoped and automatically cleared after each transaction.
    /// @return currentIndex The current action index within the transaction (0-based).
    function _getNextActionIndex() internal returns (uint256 currentIndex) {
        assembly {
            // Load current count from transient storage
            currentIndex := tload(_TRANSACTION_ACTION_COUNT_KEY)
            // Increment and store back to transient storage
            tstore(_TRANSACTION_ACTION_COUNT_KEY, add(currentIndex, 1))
        }
    }
}
